# All used properties are defined and have a label or comment
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>

SELECT DISTINCT * {
  {
    [] ?p [] .
    FILTER NOT EXISTS {
      { ?p a owl:ObjectProperty }
      UNION { ?p a owl:DatatypeProperty }
      UNION { ?p a owl:AnnotationProperty }
      UNION { ?p a owl:OntologyProperty }
      UNION { ?p a rdf:Property }
    }
  } UNION {
    [] ?p [] .
    FILTER NOT EXISTS { { ?p rdfs:label [] } UNION { ?p rdfs:comment [] } } .
  }
}
